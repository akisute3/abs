# Maintainer: akisute3 <akisute3@gmail.com>
pkgname=eclipse-3.8.2
_pkgname=eclipse
pkgver=3.8.2
pkgrel=1
_date=201301310800
pkgdesc="An IDE for Java and other languages - 3.8"
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'libwebkit' 'libxtst')
makedepends=('zip')
license=("EPL/1.1")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("http://download.eclipse.org/eclipse/downloads/drops/R-$pkgver-${_date}/${_pkgname}-SDK-$pkgver-linux-gtk.tar.gz"
  'eclipse.sh' 'eclipse.desktop' 'eclipse.svg')
md5sums=('1f640578cd89202b6ab20533750ed9ba'
         '7ea99a30fbaf06ec29261541b8eb1e23'
         'aac11c26cb87a2a7e0567aac337869a7'
         '7a0369cb6394784b651772987f51f78e')
[ "$CARCH" = "x86_64" ] && source[0]="http://download.eclipse.org/eclipse/downloads/drops/R-$pkgver-${_date}/${_pkgname}-SDK-$pkgver-linux-gtk-$CARCH.tar.gz"
[ "$CARCH" = "x86_64" ] && md5sums[0]='da92b83689eef485e39d39e62d57a8d0'

package() {
  # install eclipse
  install -m755 -d "$pkgdir/usr/share"
  mv eclipse "$pkgdir/usr/share/"

  # install misc
  install -d $pkgdir/usr/bin $pkgdir/usr/share/applications \
    $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,256x256}/apps
  install -m755 eclipse.sh "$pkgdir/usr/bin/eclipse"
  install -m644 eclipse.desktop "$pkgdir/usr/share/applications/"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_$pkgver.v${_date}/eclipse16.png \
    "$pkgdir/usr/share/icons/hicolor/16x16/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_$pkgver.v${_date}/eclipse32.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_$pkgver.v${_date}/eclipse48.png \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_$pkgver.v${_date}/eclipse256.png \
    "$pkgdir/usr/share/icons/hicolor/256x256/apps/eclipse.png"

  # install icon
  install -Dm644 "$srcdir"/eclipse.svg \
    "$pkgdir"/usr/share/icons/hicolor/scalable/apps/eclipse.svg
  sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|" "$pkgdir"/usr/share/eclipse/plugins/org.apache.ant_1.8.3.v201301120609/bin/runant.py
}
